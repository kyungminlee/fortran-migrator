program test_pzgetrf
    use prec_kinds,       only: ep
    use compare,          only: max_rel_err_mat_z
    use pblas_prec_report, only: report_init, report_case, report_finalize
    use ref_quad_lapack,  only: zgetrf
    use pblas_grid,       only: grid_init, grid_exit, my_rank, my_nprow, &
                                my_row, numroc_local, local_desc
    use pblas_distrib,    only: gen_distrib_matrix_z, gather_matrix_z, &
                                set_local_from_global_z
    use target_scalapack, only: target_name, target_eps, target_pzgetrf
    implicit none

    integer, parameter :: ns(*) = [32, 64, 96]
    integer, parameter :: mb = 8, nb = 8
    integer :: i, n, info, info_ref
    integer :: locm_a
    integer :: desca(9)
    complex(ep), allocatable :: A_loc(:,:), A_glob(:,:), A_got(:,:), A_ref(:,:)
    integer,  allocatable :: ipiv_got(:), ipiv_ref(:)
    real(ep) :: err, tol
    character(len=48) :: label
    integer :: k

    call grid_init()
    call report_init('pzgetrf', target_name, my_rank)

    do i = 1, size(ns)
        n = ns(i)
        call gen_distrib_matrix_z(n, n, mb, nb, A_loc, A_glob, seed = 13301 + 31*i)
        do k = 1, n
            A_glob(k, k) = A_glob(k, k) + cmplx(real(n, ep), 0.0_ep, ep)
            call set_local_from_global_z(k, k, A_glob(k, k), mb, nb, A_loc)
        end do

        locm_a = numroc_local(n, mb, my_row, 0, my_nprow)
        call local_desc(desca, n, n, mb, nb)

        allocate(ipiv_got(locm_a + mb))
        call target_pzgetrf(n, n, A_loc, 1, 1, desca, ipiv_got, info)
        call gather_matrix_z(n, n, mb, nb, A_loc, A_got)

        if (my_rank == 0) then
            allocate(A_ref(n, n), ipiv_ref(n))
            A_ref = A_glob
            call zgetrf(n, n, A_ref, n, ipiv_ref, info_ref)
            err = max_rel_err_mat_z(A_got, A_ref)
            tol = 32.0_ep * real(n, ep)**2 * target_eps
            write(label, '(a,i0)') 'n=', n
            call report_case(trim(label), err, tol)
            deallocate(A_ref, ipiv_ref, A_got)
        end if
        deallocate(A_loc, A_glob, ipiv_got)
    end do

    call report_finalize()
    call grid_exit()
end program test_pzgetrf
